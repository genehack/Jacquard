#! /opt/perl/bin/perl

use strict;
use warnings;
use 5.010;

use FindBin;
use lib "$FindBin::Bin/../lib";

use Jacquard::Model::KiokuDB;
use Jacquard::Schema::User;
use KiokuX::User::Util         qw/ crypt_password /;
use YAML                       qw/ LoadFile       /;

my $dsn = parse_config_file();

my( $username , $email , $password ) = prompt_for_info();

my $user = Jacquard::Schema::User->new(
  username => $username ,
  email    => $email ,
  password => crypt_password( $password ),
);

my $m = Jacquard::Model::KiokuDB->connect( $dsn , create => 1 );
{
  my $s = $m->new_scope;

  my $id = $m->insert_user( $user );

  say "STORED USER ID '$id'";
}

sub parse_config_file {
  my $file = "$FindBin::Bin/../jacquard.yaml";
  die "Can't find $file" unless -e $file;

  my $config = LoadFile( $file );

  my $dsn = $config->{'Model::KiokuDB'}{dsn}
    or die "Can't find DSN in config";

  return $dsn;
}

sub prompt_for_info {
  my @return;
  foreach ( qw/ username email password / ) {
    print uc($_),"? ";
    my $response = <STDIN>;
    chomp( $response );
    redo unless $response;
    push @return , $response;
  }

  return @return;
}
